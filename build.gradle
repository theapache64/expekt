plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.72'
    id 'java-gradle-plugin'
    id 'maven'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.8.5'
}

group 'com.theapache64'
version '0.0.1'

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"

    // JUnit:JUnit is a unit testing framework for Java, created by Erich Gamma and Kent Beck.
    testImplementation 'junit:junit:4.13'
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

javadoc.failOnError = false
task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set('javadoc')
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "theapache64"
            name "theapache64"
            email "theapache64@gmail.com"
        }
    }

    scm {
        url "https://github.com/theapache64/greetings-plugin"
    }
}

publishing {
    publications {
        mavenPublication(MavenPublication) {
            from components.java
            artifact sourcesJar {
                classifier "sources"
            }
            artifact javadocJar {
                classifier "javadoc"
            }
            groupId 'com.theapache64'
            artifactId 'expekt'
            version '0.0.1'
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'An assertion library for Kotlin')
                root.appendNode('name', 'Expekt')
                root.appendNode('url', 'https://github.com/theapache64/expekt')
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_API_KEY')
    publications = ['mavenPublication']

    pkg {
        repo = 'maven'
        name = 'expekt'
        userOrg = 'shifarshifz'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/theapache64/greetings-plugin.git'
        version {
            name = '0.0.1'
            desc = 'An assertion library for Kotlin'
            released = new Date()
        }
    }

}